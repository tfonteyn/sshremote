plugins {
    id 'com.android.application'
    id 'de.mannodermaus.android-junit5'
}

if (project.hasProperty("hbn.signing")
        && new File(project.property("hbn.signing")).exists()) {
    apply from: project.property("hbn.signing")
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.hardbacknutter.sshremote"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
        }
    }

    lintOptions {
        // allow command line builds to complete
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        // https://developer.android.com/studio/releases/#4-0-0-desugar
        coreLibraryDesugaringEnabled true
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            // Can be enabled to make Log.x work. OR, use Logger.x
            returnDefaultValues false
        }

        unitTests.all {
            reports {
                junitXml.enabled true
                html.enabled false
            }
        }
    }
    namespace 'com.hardbackcollector.sshremote'
}

dependencies {
    implementation project(path: ':ssh-client')
    debugImplementation project(path: ':ssh-client')

    // https://github.com/google/desugar_jdk_libs
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    //
    implementation 'androidx.activity:activity:1.4.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.customview:customview:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.4.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.room:room-runtime:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"

    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.5.0-beta01'


    // local tests use JUnit5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // device tests use JUnit4
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
